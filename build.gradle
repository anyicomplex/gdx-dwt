allprojects {
    if (project.name.contains("native")) {
        apply plugin: "cpp-library"

        library {
            targetMachines = [
                    machines.linux.x86_64
            ]
        }

    }
    else {
        apply plugin: "java-library"

        ext {
            gdxVersion = '1.10.0'
            gdxControllersVersion = '2.2.1'
            graalvmVersion = '22.0.0'
        }

        group 'io.github.anyicomplex'

        repositories {
            mavenCentral()
        }

        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        }

        test {
            useJUnitPlatform()
        }
    }

}

if (!project.name.contains("native")) {
    version gdxVersion

    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

subprojects {
    if (!project.name.contains("native")) {
        dependencies {
            implementation project(":")
        }
    }
}

project(":backends:lwjgl3") {
    version gdxVersion
    dependencies {
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}
